// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© zyqxd
// derived the screener logic from @ KivancOzbilgic
//@version=4

strategy("zyqxd supertrend backtest", overlay=true, margin_long=100, margin_short=100)

// Strategy
actOnAll     = input(false,              title="Act on all signals",    group="Strategy")
strat_tp1    = input(2,                  title='TP lv1 %',              group="Strategy", step=0.1) / 100
strat_tp2    = input(8,                  title='TP lv2 %',              group="Strategy", step=0.1) / 100
strat_tp3    = input(20,                 title='TP lv3 %',              group="Strategy", step=0.1) / 100
strat_sl     = input(2,                  title='Stop lv %',             group="Strategy", step=0.1) / 100

// Supertrend
src          = input(close,              title="Source",                group="Supertrend")
periods      = input(14,                 title="ATR period",            group="Supertrend", type=input.integer)
multiplier   = input(3.0,                title="ATR multiplier",        group="Supertrend", type=input.float, step=0.1)
fastTf       = input("15",               title="Fast Timeframe",        group="Supertrend", type=input.resolution)
slowTf       = input("60",               title="Slow Timeframe",        group="Supertrend", type=input.resolution)
showInverse  = input(false,              title="Show inverse atr",      group="Supertrend")
showSignals  = input(true,               title="Show signals",          group="Supertrend")

trendInfo(periods, multipler, tf) =>
    atrV   = security(syminfo.tickerid, tf, atr(periods))
    srcV   = security(syminfo.tickerid, tf, src)
    closeV = security(syminfo.tickerid, tf, close)

    up     =  srcV - (multiplier * atrV)
    up1    =  nz(up[1], up)
    up     := closeV[1] > up1 ? max(up, up1) : up

    dn     =  srcV + (multiplier * atrV)
    dn1    =  nz(dn[1], dn)
    dn     := closeV[1] < dn1 ? min(dn, dn1) : dn

    trend  =  1
    trend  := nz(trend[1], trend)
    trend  := trend == -1 and closeV > dn1 ? 1 : trend == 1 and closeV < up1 ? -1 : trend

    signal = trend != trend[1]

    [up, dn, trend, signal]

// Plot for slow supertrend
[upA, dnA, trendA, signalA] = trendInfo(periods, multiplier, slowTf)
pa = plot(ohlc4, color = color.new(color.white, 0))
aUpPlot = plot(trendA == 1 or showInverse ? upA : na,  style = plot.style_linebr, color = color.green)
aDnPlot = plot(trendA == -1 or showInverse ? dnA : na, style = plot.style_linebr, color = color.red)
fill(pa, aUpPlot, color = color.new(color.green, trendA == 1 ? 90 : 99))
fill(pa, aDnPlot, color = color.new(color.red, trendA == -1 ? 90 : 99))

// Plot for fast supertrend
[upB, dnB, trendB, signalB] = trendInfo(periods, multiplier, fastTf)
bUpPlot = plot(trendB == 1 or showInverse ? upB : na, style = plot.style_linebr, color = color.teal)
bDnPlot = plot(trendB == -1 or showInverse ? dnB : na, style = plot.style_linebr, color = color.purple)
fill(pa, bUpPlot, color = color.new(color.teal, trendB == 1 ? 90 : 99))
fill(pa, bDnPlot, color = color.new(color.purple, trendB == -1 ? 90 : 99))

// Plot signals for fast supertrend
plotshape(showSignals and signalB and trendB == 1 and (actOnAll or trendA == 1) ? upB : na,
          text = "Buy",
          location = location.absolute,
          style = shape.labelup,
          size = size.tiny,
          color = color.teal,
          textcolor = color.white)

plotshape(showSignals and signalB and trendB == -1 and (actOnAll or trendA == -1) ? dnB : na,
          text = "Sell",
          location = location.absolute,
          style = shape.labeldown,
          size = size.tiny,
          color = color.purple,
          textcolor = color.white)

longCondition = signalB and trendB == 1 and (actOnAll or trendA == 1)
shortCondition = signalB and trendB == -1 and (actOnAll or trendA == -1)

if barstate.isconfirmed
    if longCondition
        // Hard close
        strategy.close("short", when=strategy.position_size > 0, comment="Hard close short")

        // Open
        strategy.entry("long", strategy.long, when=strategy.position_size <= 0)
        strategy.exit("Long TP 1", "long", qty_percent=33, limit=strategy.position_avg_price * (1 + strat_tp1))
        strategy.exit("Long TP 2", "long", qty_percent=33, limit=strategy.position_avg_price * (1 + strat_tp2))
        strategy.exit("Long TP 3", "long", qty_percent=33, limit=strategy.position_avg_price * (1 + strat_tp3))
        strategy.exit("Long STOP", "long", stop=strategy.position_avg_price * (1 - strat_sl))

    if shortCondition
        // Hard close
        strategy.close("long", when=strategy.position_size > 0, comment="Hard close long")

        // Open
        strategy.entry("short", strategy.short, when=strategy.position_size <= 0)
        strategy.exit("Short TP 1", "short", qty_percent=33, limit=strategy.position_avg_price * (1 - strat_tp1))
        strategy.exit("Short TP 2", "short", qty_percent=33, limit=strategy.position_avg_price * (1 - strat_tp2))
        strategy.exit("Short TP 3", "short", qty_percent=33, limit=strategy.position_avg_price * (1 - strat_tp3))
        strategy.exit("Short SL", "short", stop=strategy.position_avg_price * (1 + strat_sl))

plot(strategy.position_avg_price, color=color.fuchsia)
plot(strategy.position_avg_price * (1 + strat_tp1), color=color.green)
plot(strategy.position_avg_price * (1 + strat_tp2), color=color.green)
plot(strategy.position_avg_price * (1 + strat_tp3), color=color.green)
plot(strategy.position_avg_price * (1 - strat_sl), color=color.red)
plot(strategy.netprofit)